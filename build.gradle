plugins {
    id 'java'
    id 'checkstyle'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.18'
    // id 'pmd'
    id 'jacoco'
}


def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: getCurrentGitBranch()

group 'uk.gov.hmcts.reform.probate'
// Do not change this version number
version buildNumber


checkstyle {
    maxWarnings = 0
    toolVersion = '8.5'
}

sourceCompatibility = 11

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/probate-commons"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'probate-commons'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'probate-commons'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/probate-commons'
        version {
            name = project.version
        }
    }
}

def versions = [
        jupiter           : '5.3.2',
        lombok            : '1.18.12',
        jackson           : '2.9.7',
        apacheCommons     : '3.8.1',
        hibernateValidator: '6.0.13.Final',
        springfoxSwagger  : '2.9.2',
        springBoot        : '2.1.0.RELEASE',
        commonsIo         : '2.6',
        pact_version      : '4.1.0'
]

dependencies {
    compile group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    compile group: 'org.apache.commons', name: 'commons-lang3', version: versions.apacheCommons
    compile group: 'org.hibernate.validator', name: 'hibernate-validator', version: versions.hibernateValidator
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testCompile group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    testCompile group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    testCompile group: 'org.glassfish.web', name: 'javax.el', version: '2.2.6'

    // pact
    compile group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
    // pact
    testCompile("org.junit.jupiter:junit-jupiter-api:5.3.2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.3.2")
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.3.2')
    testImplementation('org.mockito:mockito-core:2.7.22')


}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    useJUnit()
    testLogging {
        events "skipped", "failed"
    }
}

jar {
    enabled = true
}

def getCurrentGitBranch() {
    def gitBranch = "DEV-SNAPSHOT"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim()
        }
    } catch (e) {
    }
    return gitBranch
}